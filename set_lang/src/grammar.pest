main = _{ SOI ~ term* ~ EOI }

term = _{ add }

add = { sub ~ ("+" ~ sub)* }
sub = { mul ~ ("-" ~ mul)* }
mul = { div ~ ("*" ~ div)* }
div = { mod_term ~ ("/" ~ mod_term)* }
mod_term = { exp_term ~ ("%" ~ exp_term)* }

exp_term = _{ exp | factorial_term }
exp = { factorial_term ~ "^" ~ factorial_term }

factorial_term = _{ factorial | app }
factorial = { app ~ "!" }

app = _{ atom | call | image }

call = { atom ~ "(" ~ (term ~ ("," ~ term)*)? ~ ")" }
image = { atom ~ "[" ~ term ~ "]" }

atom = _{ int
        | "(" ~ term ~ ")"
        | finset | rangeset | rangeset_step
        | list
        | fun_single_arg | fun_multi_arg
        | var
        }

fun_single_arg = { var ~ "|->" ~ term }
fun_multi_arg = { "(" ~ var ~ ("," ~ var)* ~ ")" ~ "|->" ~ term }

finset = { "{" ~ (term ~ ("," ~ term)*)? ~ "}" }
list = { "[" ~ (term ~ ("," ~ term)*)? ~ "]" }
rangeset = { "{" ~ term ~ "..." ~ term ~ "}" }
rangeset_step = { "{" ~ term ~ "," ~ term ~ "..." ~ term ~ "}" }

int = @{ "-"? ~ ASCII_DIGIT+ }

is = { "are" | "is" | "∈" | "be" | ":=" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

special = { "ℕ" | "𝕀" | "ℙ" | "ℂ" | "ℚ" | "ω" | "ℝ" | "π"
          | "σ" | "τ" | "Π" | "ξ" | "μ" | "δ" | "θ" | "γ"
          | "β" | "α" | "$" | "⋃" | "⋂" | "_" }
start_char = { ASCII_ALPHA | special }
char = { ASCII_ALPHANUMERIC | special }
var = @{ start_char ~ char* }

