main = _{ SOI ~ (term ~ ".")* ~ EOI }

term = _{ definition
        | proof_rule
        | rule
        | ite
        | sum
        | bool_term }

definition = { "Let" ~ var ~ is ~ term }
rule = { "Rule" ~ term ~ "=>" ~ term }
proof_rule = { "Proof" ~ "Rule" ~ term ~ "=>" ~ term }

ite = { "if" ~ term ~ "then" ~ term ~ "else" ~ term }

sum = { sum_sym ~ term }
sum_sym = _{ "Î£" | "âˆ‘" }

bool_term = { complement_term ~ (infix_bool_op ~ complement_term)* }
infix_bool_op = _{ elem | and_op | or_op }
elem = { "âˆˆ" }
and_op = { "and" }
or_op = { "or" }

complement_term = _{ complement | compare_term }
complement = { ("not" | "!") ~ compare_term }
compare_term = _{ compare | arith }
compare = { arith ~ compare_op ~ arith }
compare_op = _{ ne_sym | eq_sym | le_sym | lt_sym | ge_sym | gt_sym }

lt_sym = { "<" }
le_sym = { "<=" | "â‰¤" }
gt_sym = { ">" }
ge_sym = { ">=" | "â‰¥" }
eq_sym = { "=" }
ne_sym = { "!=" | "â‰ " }

conj_sym = _{ "&" | "/\\" | "âˆ§" | "and" }
disj_sym = { "|" | "\\/" | "âˆ¨" | "or" }

arith = _{ negate | bin_arith }
negate = { "-" ~ bin_arith }

bin_arith = { factorial_term ~ (bin_op ~ factorial_term)* }

bin_op = _{ add | sub | mul | div | mod_op | exp | concat }
add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
mod_op = { "%" }
exp = { "^" }
concat = { "@" }

factorial_term = _{ factorial | app }
factorial = { app ~ "!" }

app = _{ call | image | atom }

call = { atom ~ "(" ~ (term ~ ("," ~ term)*)? ~ ")" }
image = { atom ~ "[" ~ term ~ "]" }

atom = _{ int
        | "(" ~ term ~ ")"
        | finset | rangeset | rangeset_step | comp_set
        | list
        | fun_single_arg | fun_multi_arg
        | var
        }

fun_single_arg = { var ~ "|->" ~ term }
fun_multi_arg = { "(" ~ var ~ ("," ~ var)* ~ ")" ~ "|->" ~ term }

finset = { "{" ~ (term ~ ("," ~ term)*)? ~ "}" }
list = { "[" ~ (term ~ ("," ~ term)*)? ~ "]" }
rangeset = { "{" ~ term ~ "..." ~ term ~ "}" }
rangeset_step = { "{" ~ term ~ "," ~ term ~ "..." ~ term ~ "}" }
comp_set = { "{" ~ term ~ ":" ~ term ~ ("," ~ term)* ~ "}" }

int = @{ "-"? ~ ASCII_DIGIT+ }

is = _{ "are" | "is" | "be" | ":=" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

special = { "â„•" | "ğ•€" | "â„™" | "â„‚" | "â„š" | "Ï‰" | "â„" | "Ï€"
          | "Ïƒ" | "Ï„" | "Î " | "Î¾" | "Î¼" | "Î´" | "Î¸" | "Î³"
          | "Î²" | "Î±" | "$" | "â‹ƒ" | "â‹‚" | "_" }
start_char = { ASCII_ALPHA | special }
char = { ASCII_ALPHANUMERIC | special }
var = @{ start_char ~ char* }

